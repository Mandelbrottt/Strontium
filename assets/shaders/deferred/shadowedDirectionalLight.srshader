#type common
#version 440
/*
 * PBR shader program for an occluded directional light. Follows the Filament
 * material system (somewhat).
 * https://google.github.io/filament/Filament.md.html#materialsystem/standardmodel
 */

#type vertex
void main()
{
  vec2 position = vec2(gl_VertexID % 2, gl_VertexID / 2) * 4.0 - 1;

  gl_Position = vec4(position, 0.0, 1.0);
}

#type fragment
#define PI 3.141592654

#define NUM_CASCADES 4
#define REGION 5.0
#define WARP 44.0

#define PCF_SAMPLES 16
#define PCSS_SAMPLES 16

// Camera specific uniforms.
layout(std140, binding = 0) uniform CameraBlock
{
  mat4 u_viewMatrix;
  mat4 u_projMatrix;
  mat4 u_invViewProjMatrix;
  vec3 u_camPosition;
  vec4 u_nearFar; // Near plane (x), far plane (y). z and w are unused.
};

// The post processing properties.
layout(std140, binding = 1) uniform PostProcessBlock
{
  vec4 u_camPosScreenSize; // Camera position (x, y, z) and the screen width (w).
  vec4 u_screenSizeGammaBloom;  // Screen height (x), gamma (y) and bloom intensity (z). w is unused.
  ivec4 u_postProcessingPasses; // Tone mapping operator (x), using bloom (y), using FXAA (z) and using sunshafts (w).
};

// Directional light uniforms.
layout(std140, binding = 5) uniform DirectionalBlock
{
  vec4 u_lColourIntensity;
  vec4 u_lDirection;
  ivec4 u_directionalSettings; // Shadow quality (x). y, z and w are unused.
};

layout(std140, binding = 7) uniform CascadedShadowBlock
{
  mat4 u_lightVP[NUM_CASCADES];
  vec4 u_cascadeData[NUM_CASCADES]; // Cascade split distance (x). y, z and w are unused.
  vec4 u_shadowParams1; // Light bleed reduction (x), light size for PCSS (y), minimum PCF radius (z) and the normal bias (w).
  vec4 u_shadowParams2; // The constant bias (x). y, z and w are unused.
};

// Uniforms for the geometry buffer.
layout(binding = 1) uniform sampler2D godrayTex;
layout(binding = 2) uniform sampler2D brdfLookUp;
layout(binding = 3) uniform sampler2D gDepth;
layout(binding = 4) uniform sampler2D gNormal;
layout(binding = 5) uniform sampler2D gAlbedo;
layout(binding = 6) uniform sampler2D gMatProp;
layout(binding = 7) uniform sampler2D cascadeMaps[NUM_CASCADES]; // TODO: Texture arrays.

// Output colour variable.
layout(location = 0) out vec4 fragColour;

// PBR BRDF.
vec3 filamentBRDF(vec3 l, vec3 v, vec3 n, float roughness, float metallic,
                  vec3 dielectricF0, vec3 metallicF0, vec3 f90, vec3 diffuseAlbedo);
// Shadow calculations.
float calcShadow(uint cascadeIndex, vec3 position, vec3 normal, vec3 lightDir,
                 int quality);

// Decodes the worldspace position of the fragment from depth.
vec3 decodePosition(vec2 texCoords, sampler2D depthMap, mat4 invMVP)
{
  float depth = texture(depthMap, texCoords).r;
  vec3 clipCoords = 2.0 * vec3(texCoords, depth) - 1.0.xxx;
  vec4 temp = invMVP * vec4(clipCoords, 1.0);
  return temp.xyz / temp.w;
}

// Fast octahedron normal vector decoding.
// https://jcgt.org/published/0003/02/01/
vec2 signNotZero(vec2 v)
{
  return vec2((v.x >= 0.0) ? 1.0 : -1.0, (v.y >= 0.0) ? 1.0 : -1.0);
}
vec3 decodeNormal(vec2 texCoords, sampler2D encodedNormals)
{
  vec2 e = texture(encodedNormals, texCoords).xy;
  vec3 v = vec3(e.xy, 1.0 - abs(e.x) - abs(e.y));
  if (v.z < 0)
    v.xy = (1.0 - abs(v.yx)) * signNotZero(v.xy);
  return normalize(v);
}

// Bicubic hermite texture sampling.
// https://www.shadertoy.com/view/MllSzX
vec4 bicubicHermiteTextureSample(sampler2D tex, vec2 uv);

void main()
{
  vec2 fTexCoords = gl_FragCoord.xy / textureSize(gDepth, 0).xy;

  vec3 position = decodePosition(fTexCoords, gDepth, u_invViewProjMatrix);
  vec3 normal = decodeNormal(fTexCoords, gNormal);
  vec4 albedoReflectance = texture(gAlbedo, fTexCoords);
  vec3 albedo = albedoReflectance.rgb;
  vec2 mr = texture(gMatProp, fTexCoords).rg;
  float metallic = mr.r;
  float roughness = mr.g;

  // Remap material properties.
  vec3 metallicF0 = albedo * metallic;
  vec3 dielectricF0 = 0.16 * albedoReflectance.aaa * albedoReflectance.aaa;

  // Dirty, setting f90 to 1.0.
  vec3 f90 = vec3(1.0);

  // Light properties.
  vec3 view = normalize(u_camPosition - position);
  vec3 light = normalize(u_lDirection.xyz);
  vec3 lightColour = u_lColourIntensity.xyz;
  float lightIntensity = u_lColourIntensity.w;
  float nDotL = clamp(dot(normal, light), 0.0, 1.0);

  // Cascaded shadow mapping.
  vec4 clipSpacePos = u_viewMatrix * vec4(position, 1.0);
  float shadowFactor = 1.0;
  for (uint i = 0; i < NUM_CASCADES; i++)
  {
    if (-clipSpacePos.z <= (u_cascadeData[i].x))
    {
      // Interpolate between the current and the next cascade to smooth the
      // transition between shadows.
      shadowFactor = calcShadow(i, position, normal, light, u_directionalSettings.x);

      float currentSplit = i == 0 ? 0.0 : u_cascadeData[i - 1].x;
      float nextSplit = u_cascadeData[i].x;
      float splitDelta = nextSplit - currentSplit;
      float fadeFactor = (nextSplit + clipSpacePos.z) / splitDelta;

      if (i < (NUM_CASCADES - 1))
      {
        float nextShadowFactor = calcShadow(i + 1, position, normal, light, u_directionalSettings.x);
        float lerpFactor = smoothstep(0.0, 1.0, fadeFactor);
        shadowFactor = mix(nextShadowFactor, shadowFactor, lerpFactor);
      }

      break;
    }
  }

  // Add on screen-space godrays.
  vec3 radiance = vec3(0.0);
  float transmittance = 1.0;
  if (u_postProcessingPasses.w != 0)
  {
    // Bicubic upsampling to AA in the u and v direction as described in [Patry2021]:
    // http://advances.realtimerendering.com/s2021/jpatry_advances2021/index.html
    vec4 godrays = bicubicHermiteTextureSample(godrayTex, fTexCoords);
    radiance += godrays.rgb * godrays.a * lightIntensity * lightColour;

    // Compute transmittance from the stored opacity.
    transmittance = 1.0 - godrays.a;
  }

  // Compute the radiance contribution.
  vec3 lRadiance = filamentBRDF(light, view, normal, roughness, metallic,
                                dielectricF0, metallicF0, f90, albedo);
  lRadiance *= (lightIntensity * nDotL * lightColour * shadowFactor * transmittance);

  fragColour = vec4(radiance + lRadiance, 1.0);
}

//------------------------------------------------------------------------------
// Filament PBR.
//------------------------------------------------------------------------------
// Normal distribution function.
float nGGX(float nDotH, float actualRoughness)
{
  float a = nDotH * actualRoughness;
  float k = actualRoughness / (1.0 - nDotH * nDotH + a * a);
  return k * k * (1.0 / PI);
}

// Fast visibility term. Incorrect as it approximates the two square roots.
float vGGXFast(float nDotV, float nDotL, float actualRoughness)
{
  float a = actualRoughness;
  float vVGGX = nDotL * (nDotV * (1.0 - a) + a);
  float lVGGX = nDotV * (nDotL * (1.0 - a) + a);
  return 0.5 / max(vVGGX + lVGGX, 1e-5);
}

// Schlick approximation for the Fresnel factor.
vec3 sFresnel(float vDotH, vec3 f0, vec3 f90)
{
  float p = 1.0 - vDotH;
  return f0 + (f90 - f0) * p * p * p * p * p;
}

// Cook-Torrance specular for the specular component of the BRDF.
vec3 fsCookTorrance(float nDotH, float lDotH, float nDotV, float nDotL,
                    float vDotH, float actualRoughness, vec3 f0, vec3 f90)
{
  float D = nGGX(nDotH, actualRoughness);
  vec3 F = sFresnel(vDotH, f0, f90);
  float V = vGGXFast(nDotV, nDotL, actualRoughness);
  return D * F * V;
}

// Burley diffuse for the diffuse component of the BRDF.
vec3 fdBurley(float nDotV, float nDotL, float lDotH, float actualRoughness, vec3 diffuseAlbedo)
{
  vec3 f90 = vec3(0.5 + 2.0 * actualRoughness * lDotH * lDotH);
  vec3 lightScat = sFresnel(nDotL, vec3(1.0), f90);
  vec3 viewScat = sFresnel(nDotV, vec3(1.0), f90);
  return lightScat * viewScat * (1.0 / PI) * diffuseAlbedo;
}

// Lambertian diffuse for the diffuse component of the BRDF.
vec3 fdLambert(vec3 diffuseAlbedo)
{
  return diffuseAlbedo / PI;
}

// Lambertian diffuse for the diffuse component of the BRDF. Corrected to guarantee
// energy is conserved.
vec3 fdLambertCorrected(vec3 f0, vec3 f90, float vDotH, float lDotH,
                        vec3 diffuseAlbedo)
{
  // Making the assumption that the external medium is air (IOR of 1).
  vec3 iorExtern = vec3(1.0);
  // Calculating the IOR of the medium using f0.
  vec3 iorIntern = (vec3(1.0) - sqrt(f0)) / (vec3(1.0) + sqrt(f0));
  // Ratio of the IORs.
  vec3 iorRatio = iorExtern / iorIntern;

  // Compute the incoming and outgoing Fresnel factors.
  vec3 fIncoming = sFresnel(lDotH, f0, f90);
  vec3 fOutgoing = sFresnel(vDotH, f0, f90);

  // Compute the fraction of light which doesn't get reflected back into the
  // medium for TIR.
  vec3 rExtern = PI * (20.0 * f0 + 1.0) / 21.0;
  // Use rExtern to compute the fraction of light which gets reflected back into
  // the medium for TIR.
  vec3 rIntern = vec3(1.0) - (iorRatio * iorRatio * (vec3(1.0) - rExtern));

  // The TIR contribution.
  vec3 tirDiffuse = vec3(1.0) - (rIntern * diffuseAlbedo);

  // The final diffuse BRDF.
  return (iorRatio * iorRatio) * diffuseAlbedo * (vec3(1.0) - fIncoming) * (vec3(1.0) - fOutgoing) / (PI * tirDiffuse);
}

// The final combined BRDF. Compensates for energy gain in the diffuse BRDF and
// energy loss in the specular BRDF.
vec3 filamentBRDF(vec3 l, vec3 v, vec3 n, float roughness, float metallic,
                  vec3 dielectricF0, vec3 metallicF0, vec3 f90,
                  vec3 diffuseAlbedo)
{
  vec3 h = normalize(v + l);

  float nDotV = max(abs(dot(n, v)), 1e-5);
  float nDotL = clamp(dot(n, l), 1e-5, 1.0);
  float nDotH = clamp(dot(n, h), 1e-5, 1.0);
  float lDotH = clamp(dot(l, h), 1e-5, 1.0);
  float vDotH = clamp(dot(v, h), 1e-5, 1.0);

  float clampedRoughness = max(roughness, 0.045);
  float actualRoughness = clampedRoughness * clampedRoughness;

  vec2 dfg = texture(brdfLookUp, vec2(nDotV, roughness)).rg;
  dfg = max(dfg, 1e-4.xx);
  vec3 energyDielectric = 1.0.xxx + dielectricF0 * (1.0.xxx / dfg.y - 1.0.xxx);
  vec3 energyMetallic = 1.0.xxx + metallicF0 * (1.0.xxx / dfg.y - 1.0.xxx);

  vec3 fs = fsCookTorrance(nDotH, lDotH, nDotV, nDotL, vDotH, actualRoughness, dielectricF0, f90);
  //fs *= energyDielectric;
  vec3 fd = fdLambertCorrected(dielectricF0, f90, vDotH, lDotH, diffuseAlbedo);
  vec3 dielectricBRDF = fs + fd;

  vec3 metallicBRDF = fsCookTorrance(nDotH, lDotH, nDotV, nDotL, vDotH, actualRoughness, metallicF0, f90);
  //metallicBRDF *= energyMetallic;

  return mix(dielectricBRDF, metallicBRDF, metallic);
}

//------------------------------------------------------------------------------
// Shadow helper functions.
//------------------------------------------------------------------------------
// Interleaved gradient noise from:
// http://www.iryoku.com/next-generation-post-processing-in-call-of-duty-advanced-warfare
float interleavedGradientNoise()
{
  const vec3 magic = vec3(0.06711056, 0.00583715, 52.9829189);
  return fract(magic.z * fract(dot(gl_FragCoord.xy, magic.xy)));
}

// Create a rotation matrix with the noise function above.
mat2 randomRotation()
{
  float theta = 2.0 * PI * interleavedGradientNoise();
  float sinTheta = sin(theta);
  float cosTheta = cos(theta);
  return mat2(cosTheta, sinTheta, -sinTheta, cosTheta);
}

const vec2 poissonDisk[64] = vec2[64]
(
  vec2(-0.613392, 0.617481),
  vec2(0.170019, -0.040254),
  vec2(-0.299417, 0.791925),
  vec2(0.645680, 0.493210),
  vec2(-0.651784, 0.717887),
  vec2(0.421003, 0.027070),
  vec2(-0.817194, -0.271096),
  vec2(-0.705374, -0.668203),
  vec2(0.977050, -0.108615),
  vec2(0.063326, 0.142369),
  vec2(0.203528, 0.214331),
  vec2(-0.667531, 0.326090),
  vec2(-0.098422, -0.295755),
  vec2(-0.885922, 0.215369),
  vec2(0.566637, 0.605213),
  vec2(0.039766, -0.396100),
  vec2(0.751946, 0.453352),
  vec2(0.078707, -0.715323),
  vec2(-0.075838, -0.529344),
  vec2(0.724479, -0.580798),
  vec2(0.222999, -0.215125),
  vec2(-0.467574, -0.405438),
  vec2(-0.248268, -0.814753),
  vec2(0.354411, -0.887570),
  vec2(0.175817, 0.382366),
  vec2(0.487472, -0.063082),
  vec2(-0.084078, 0.898312),
  vec2(0.488876, -0.783441),
  vec2(0.470016, 0.217933),
  vec2(-0.696890, -0.549791),
  vec2(-0.149693, 0.605762),
  vec2(0.034211, 0.979980),
  vec2(0.503098, -0.308878),
  vec2(-0.016205, -0.872921),
  vec2(0.385784, -0.393902),
  vec2(-0.146886, -0.859249),
  vec2(0.643361, 0.164098),
  vec2(0.634388, -0.049471),
  vec2(-0.688894, 0.007843),
  vec2(0.464034, -0.188818),
  vec2(-0.440840, 0.137486),
  vec2(0.364483, 0.511704),
  vec2(0.034028, 0.325968),
  vec2(0.099094, -0.308023),
  vec2(0.693960, -0.366253),
  vec2(0.678884, -0.204688),
  vec2(0.001801, 0.780328),
  vec2(0.145177, -0.898984),
  vec2(0.062655, -0.611866),
  vec2(0.315226, -0.604297),
  vec2(-0.780145, 0.486251),
  vec2(-0.371868, 0.882138),
  vec2(0.200476, 0.494430),
  vec2(-0.494552, -0.711051),
  vec2(0.612476, 0.705252),
  vec2(-0.578845, -0.768792),
  vec2(-0.772454, -0.090976),
  vec2(0.504440, 0.372295),
  vec2(0.155736, 0.065157),
  vec2(0.391522, 0.849605),
  vec2(-0.620106, -0.328104),
  vec2(0.789239, -0.419965),
  vec2(-0.545396, 0.538133),
  vec2(-0.178564, -0.596057)
);

vec2 samplePoissonDisk(uint index)
{
  return poissonDisk[index % 64];
}

//------------------------------------------------------------------------------
// Shadow calculations for exponentially warped variance shadow maps.
//------------------------------------------------------------------------------
// Warp the depth.
vec2 warpDepth(float depth)
{
  float posWarp = exp(WARP * depth);
  float negWarp = -1.0 * exp(-1.0 * WARP * depth);
  return vec2(posWarp, negWarp);
}

// Compute the Chebyshev bounds to determine if the fragment is in shadow or not.
float computeChebyshevBound(float moment1, float moment2, float depth)
{
  float variance2 = moment2 - moment1 * moment1;
  float diff = depth - moment1;
  float diff2 = diff * diff;
  float pMax = clamp((variance2 / (variance2 + diff2) - u_shadowParams1.x) / (1.0 - u_shadowParams1.x), 0.0, 1.0);

  return moment1 < depth ? pMax : 1.0;
}

//------------------------------------------------------------------------------
// Shadow calculations for PCF shadow maps.
//------------------------------------------------------------------------------
float offsetLookup(sampler2D map, vec2 loc, vec2 offset, float depth)
{
  vec2 texel = 1.0 / textureSize(map, 0).xy;
  return float(texture(map, loc.xy + offset * texel).r >= depth);
}

//------------------------------------------------------------------------------
// Shadow calculations for PCSS shadow maps.
//------------------------------------------------------------------------------
float calculateSearchWidth(float receiverDepth, float lightSize, vec2 texelSize)
{
  return lightSize * receiverDepth;
}

float calcBlockerDistance(sampler2D map, vec3 projCoords, float bias, float lightSize)
{
  vec2 texel = 1.0 / textureSize(map, 0).xy;
  float blockerDistances = 0.0;
  float numBlockerDistances = 0.0;
  float receiverDepth = projCoords.z;

  float searchWidth = calculateSearchWidth(receiverDepth, lightSize, texel);
  mat2 rotation = randomRotation();
  for (uint i = 0; i < PCSS_SAMPLES; i++)
  {
    vec2 disk = searchWidth * samplePoissonDisk(i);
    disk = rotation * disk;

    float blockerDepth = texture(map, projCoords.xy + disk * texel).r;
    if (blockerDepth < projCoords.z - bias)
    {
      numBlockerDistances += 1.0;
      blockerDistances += blockerDepth;
    }
  }

  if (numBlockerDistances > 0.0)
    return blockerDistances / numBlockerDistances;
  else
    return -1.0;
}

float calcPCFKernelSize(sampler2D map, vec3 projCoords, float bias, float lightSize)
{
  float receiverDepth = projCoords.z;
  float blockerDepth = calcBlockerDistance(map, projCoords, bias, lightSize);

  float kernelSize = 1.0;
  if (blockerDepth > 0.0)
  {
    float penumbraWidth = lightSize * (receiverDepth - blockerDepth) / blockerDepth;
    kernelSize = penumbraWidth / (receiverDepth);
  }

  return kernelSize;
}

//------------------------------------------------------------------------------
// Compute the actual occlusion factor.
//------------------------------------------------------------------------------
float calcShadow(uint cascadeIndex, vec3 position, vec3 normal, vec3 lightDir,
                 int quality)
{
  float depthRange = 0.0;
  if (cascadeIndex == 0)
  {
    depthRange = u_cascadeData[cascadeIndex].x;
  }
  else
  {
    depthRange = u_cascadeData[cascadeIndex].x - u_cascadeData[cascadeIndex - 1].x;
  }

  float bias = u_shadowParams2.x / 1000.0;

  float normalOffsetScale = clamp(1.0 - dot(normal, lightDir), 0.0, 1.0);
  normalOffsetScale /= textureSize(cascadeMaps[cascadeIndex], 0).x;
  normalOffsetScale *= depthRange * u_shadowParams1.w;

  vec4 shadowOffset = vec4(position + normal * normalOffsetScale, 1.0);
  shadowOffset = u_lightVP[cascadeIndex] * shadowOffset;

  vec4 lightClipPos = u_lightVP[cascadeIndex] * vec4(position, 1.0);
  if (quality != 2)
    lightClipPos.xy = shadowOffset.xy;
  vec3 projCoords = lightClipPos.xyz / lightClipPos.w;
  projCoords = 0.5 * projCoords + 0.5;

  // PCSS (contact hardening shadows), ultra quality.
  float shadowFactor = 1.0;
  if (quality == 3)
  {
    shadowFactor = 0.0;
    float radius = calcPCFKernelSize(cascadeMaps[cascadeIndex], projCoords, bias, u_shadowParams1.y);
    radius += u_shadowParams1.z;
    mat2 rotation = randomRotation();
    for (uint i = 0; i < PCF_SAMPLES; i++)
    {
      vec2 disk = radius * samplePoissonDisk(i);
      disk = rotation * disk;

      shadowFactor += offsetLookup(cascadeMaps[cascadeIndex], projCoords.xy,
                                   disk, projCoords.z - bias);
    }

    shadowFactor /= float(PCF_SAMPLES);
  }
  // EVSM shadows, best quality with no contact hardening.
  else if (quality == 2)
  {
    vec4 moments = texture(cascadeMaps[cascadeIndex], projCoords.xy).rgba;
    vec2 warpedDepth = warpDepth(projCoords.z - 1e-4);

    float shadowFactor1 = computeChebyshevBound(moments.r, moments.g, warpedDepth.r);
    float shadowFactor2 = computeChebyshevBound(moments.b, moments.a, warpedDepth.g);
    shadowFactor = min(shadowFactor1, shadowFactor2);
  }
  // PCF shadows, medium quality.
  else if (quality == 1)
  {
    shadowFactor = 0.0;
    float radius = u_shadowParams1.z;
    mat2 rotation = randomRotation();
    for (uint i = 0; i < PCF_SAMPLES; i++)
    {
      vec2 disk = radius * samplePoissonDisk(i);
      disk = rotation * disk;
      shadowFactor += offsetLookup(cascadeMaps[cascadeIndex], projCoords.xy,
                                   disk, projCoords.z - bias);
    }

    shadowFactor /= float(PCF_SAMPLES);
  }
  // Hard shadows, low quality.
  else if (quality == 0)
  {
    shadowFactor = offsetLookup(cascadeMaps[cascadeIndex], projCoords.xy, 0.0.xx,
                                projCoords.z - bias);
  }

  return shadowFactor;
}

// Bicubic hermite texture sampling.
// https://www.shadertoy.com/view/MllSzX
vec4 cubicHermite(vec4 A, vec4 B, vec4 C, vec4 D, float t)
{
  float t2 = t * t;
  float t3 = t * t * t;
  vec4 a = -A / 2.0 + (3.0 * B) / 2.0 - (3.0 * C) / 2.0 + D / 2.0;
  vec4 b = A - (5.0 * B) / 2.0 + 2.0 * C - D / 2.0;
  vec4 c = -A / 2.0 + C / 2.0;
  vec4 d = B;

  return a * t3 + b * t2 + c * t + d;
}
vec4 bicubicHermiteTextureSample(sampler2D tex, vec2 uv)
{
  vec2 texSize = vec2(textureSize(tex, 0).xy);
  vec2 c_onePixel = 1.0.xx / texSize;
  vec2 c_twoPixels = 2.0 * c_onePixel;

  vec2 pixel = uv * texSize;

  vec2 frac = fract(pixel);
  pixel = floor(pixel) / texSize - vec2(c_onePixel / 2.0);

  vec4 C00 = texture(tex, pixel - c_onePixel);
  vec4 C10 = texture(tex, pixel + vec2(0.0, -c_onePixel.y));
  vec4 C20 = texture(tex, pixel + vec2(c_onePixel.x, -c_onePixel.y));
  vec4 C30 = texture(tex, pixel + vec2(c_twoPixels.x, -c_onePixel.y));

  vec4 C01 = texture(tex, pixel + vec2(-c_onePixel.x, 0.0));
  vec4 C11 = texture(tex, pixel + vec2(0.0, 0.0));
  vec4 C21 = texture(tex, pixel + vec2(c_onePixel.x, 0.0));
  vec4 C31 = texture(tex, pixel + vec2(c_twoPixels.x, 0.0));

  vec4 C02 = texture(tex, pixel + vec2(-c_onePixel.x, c_onePixel.y));
  vec4 C12 = texture(tex, pixel + vec2(0.0, c_onePixel.y));
  vec4 C22 = texture(tex, pixel + vec2(c_onePixel.x, c_onePixel.y));
  vec4 C32 = texture(tex, pixel + vec2(c_twoPixels.x, c_onePixel.y));

  vec4 C03 = texture(tex, pixel + vec2(-c_onePixel.x, c_twoPixels.y));
  vec4 C13 = texture(tex, pixel + vec2(0.0, c_twoPixels.y));
  vec4 C23 = texture(tex, pixel + vec2(c_onePixel.x, c_twoPixels.y));
  vec4 C33 = texture(tex, pixel + vec2(c_twoPixels.x, c_twoPixels.y));

  vec4 CP0X = cubicHermite(C00, C10, C20, C30, frac.x);
  vec4 CP1X = cubicHermite(C01, C11, C21, C31, frac.x);
  vec4 CP2X = cubicHermite(C02, C12, C22, C32, frac.x);
  vec4 CP3X = cubicHermite(C03, C13, C23, C33, frac.x);

  return cubicHermite(CP0X, CP1X, CP2X, CP3X, frac.y);
}
